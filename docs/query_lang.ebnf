{* This file contains the EBNF specification of greatday's todo query language (e.g. used by 'greatday list'). *}

start = [spec_or];
spec_or = spec_and {space "|" space spec_and};
spec_and = spec {space spec};
spec = create
     | desc
     | done
     | metatag
     | priority
     | tag
     | x_or_o;

{* variables required by spec... *}
create = "^" date [":" date];
desc = ["!"] ('"' NOT_DOUBLE_QUOTE+ '"' | "'" NOT_SINGLE_QUOTE+ "'");
done = "$" date [":" date];
metatag = ["!"] word | word ["?"] op any;
priority = "(" letter_range {"," letter_range} ")";
tag = ["!"] ("@" | "#" | "+") word;
x_or_o = "X" | "O";

{* all other non-special variables... *}
any = (letter | DIGIT | "-" | ".")+;
date = 4*DIGIT "-" 2*DIGIT "-" 2*DIGIT;
letter = UPPER_LETTER | LOWER_LETTER;
op = "=" | "!=" | "<=" | ">=" | "<" | ">";
letter_range = UPPER_LETTER | UPPER_LETTER "-" UPPER_LETTER;
space = " ";
word = letter+;

{* all special variables... *}
DIGIT = ? an integer between 0-9 ?;
LOWER_LETTER = ? a lowercase letter (i.e. a-z) ?;
NOT_DOUBLE_QUOTE = ? any character NOT including a double quote ?;
NOT_SINGLE_QUOTE = ? any character NOT including a single quote ?;
UPPER_LETTER = ? an uppercase letter (i.e. A-Z) ?;
