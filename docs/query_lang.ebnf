{* This file contains the EBNF specification of greatday's todo query language (e.g. used by 'greatday list'). *}

start = [spec_or];
spec_or = spec_and {space "|" space spec_and};
spec_and = spec {space spec};
spec = desc
     | metatag
     | priority
     | tag;

{* variables required by spec... *}
desc = ["!"] ('"' NOT_DOUBLE_QUOTE+ '"' | "'" NOT_SINGLE_QUOTE+ "'");
metatag = ["!"] word | word ["?"] op any;
priority = "(" UPPER_LETTER ")";
tag = ["!"] ("@" | "#" | "+") word;

{* all other non-special variables... *}
any = (letter | DIGIT | "-")+;
letter = UPPER_LETTER | LOWER_LETTER;
op = "=" | "!=" | "<=" | ">=" | "<" | ">";
space = " ";
word = letter+;

{* all special variables... *}
DIGIT = ? an integer between 0-9 ?;
LOWER_LETTER = ? a lowercase letter a-z ?;
NOT_DOUBLE_QUOTE = ? any sequence of characters not including a double quote ?;
NOT_SINGLE_QUOTE = ? any sequence of characters not including a single quote ?;
UPPER_LETTER = ? an uppercase letter A-Z ?;
