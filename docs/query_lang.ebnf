{* This file contains the EBNF specification of greatday's todo query language (e.g. used by 'greatday list'). *}

start = [spec_group {"|" spec_group}];
spec_group = spec {SPACE spec};
spec = desc
     | metatag
     | priority
     | tag;

{* variables required by spec... *}
desc = ["!"] '"' WORD {SPACE WORD} '"';
metatag = ["!"] WORD | WORD [["?"] op WORD];
priority = "(" UPPERCASE_LETTER ")";
tag = ["!"] ("@" | "#" | "+") WORD;

{* all other non-terminal variables... *}
date_range = date [":" date];
date = 4*NAT "-" 2*NAT "-" 2*NAT;
op = "=" | "!=" | "<=" | ">=" | "<" | ">";

{* all terminal variables... *}
NAT = ? a positive non-zero integer (i.e. a natural number) ?;
SPACE = " ";
UPPERCASE_LETTER = ? any uppercase letter A-Z ?;
WORD = ? any (almost) sequence of non-whitespace characters ?;
